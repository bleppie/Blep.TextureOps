#include "TextureCommon.hlsl"

#pragma kernel Circle
#pragma kernel CircleI
#pragma kernel Ellipse
#pragma kernel EllipseI
#pragma kernel Rectangle
#pragma kernel RectangleI
#pragma kernel Line
#pragma kernel LineI

Texture2D<float4> SrcA;
Texture2D<float4> SrcB;
float4 ScalarA;
float4 ScalarB;
float4 ScalarC;
float4 ScalarD;
RWTexture2D<float4> Dst;

float CircleSdf(float2 xy, float2 center, float radius) {
    return distance(xy, center) - radius;
}

float EllipseSdf(float2 xy, float2 center, float2 radius) {
    // See https://github.com/0xfaded/ellipse_demo/issues/1
    float2 p = abs(xy - center);

    float rdiff = radius.x * radius.x - radius.y * radius.y;

    // Initial guess
    float2 t = float2(0.70710678118f, 0.70710678118f); // Normalized
    float2 q = radius * t; // Closest point on ellipse

    for (int i = 0; i < 3; ++i) {
        float2 e = float2(rdiff, -rdiff) * t * t * t / radius;

        float2 qe = q - e;
        float2 pe = p - e;

        t = saturate((normalize(pe) * length(qe) + e) / radius);
        t = normalize(t);
        q = radius * t;
    }

    float df = distance(p, q); // unsigned distance function
    return sign(dot(p, p) - dot(q, q)) * df;
}

float RectangleSdf(float2 xy, float2 p, float2 q) {
    float2 size = abs(p - q);
    float2 center = (p + q) * 0.5f;
    float2 delta = abs(xy - center) - size * 0.5f;
    return max(delta.x, delta.y);
}

float LineSdf(float2 xy, float2 p, float2 q, float width) {
    float halfWidth = width  * 0.5f;

    // Project uv onto the line p-q,, clamp to (0, 1)
    float2 pq = p - q;
    float lenSqr = dot(pq, pq);
    float t = lenSqr < 0.0000001f ? 0 : dot(xy - q, pq) / lenSqr;
    float2 closest = q + saturate(t) * pq;
    return distance(xy, closest) - halfWidth;
}

float4 ColorFromSdf(float sdf, float4 bgColor, float4 fillColor,
                    float4 outlineColor, float outlineWidth) {

    // Magic that does antialiasing
    // distOuter = dist
    // distInner = dist + OutlineWidth
    //   distInner < 0  -->  FillColor
    //   distInner < 1  -->  lerp between FillColor and OutlineColor
    //   distOuter < 0  -->  OutlineColor
    //   distOuter < 1  -->  OutlineColor with alpha between 1 and 0
    //   distOuter > 1  -->  OutlineColor with alpha 0
    float4 color = lerp(fillColor, outlineColor, saturate(sdf + outlineWidth));
    color.a *= 1 - saturate(sdf);

    // Blend over bgColor
    color.rgb = lerp(bgColor.rgb, color.rgb, color.a);
    color.a = color.a + (1 - color.a) * bgColor.a;
    return color;
}

float4 ColorFromSdf(float sdf, float4 bgColor) {
    float4 fillColor = ScalarA;
    float4 outlineColor = ScalarB;
    float outlineWidth = ScalarC.x;
    return ColorFromSdf(sdf, bgColor, fillColor, outlineColor, outlineWidth);

}

// Sets dst to ScalarA in the circle described by ScalarB (ctr.x, ctr.y, rad, falloff)
[numthreads(THREADSX, THREADSY, 1)]
void Circle(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float2 center = ScalarD.xy;
        float radius = ScalarD.z;
        float sdf = CircleSdf(xy, center, radius);
        Dst[xy] = ColorFromSdf(sdf, SrcA[xy]);
    }
}
// Sets dst to ScalarA in the circle described by ScalarB (ctr.x, ctr.y, rad, falloff)
[numthreads(THREADSX, THREADSY, 1)]
void CircleI(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float2 center = ScalarD.xy;
        float radius = ScalarD.z;
        float sdf = CircleSdf(xy, center, radius);
        Dst[xy] = ColorFromSdf(sdf, Dst[xy]);
    }
}

[numthreads(THREADSX, THREADSY, 1)]
void Ellipse(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float2 center = ScalarD.xy;
        float2 radius = ScalarD.zw;
        float sdf = EllipseSdf(xy, center, radius);
        Dst[xy] = ColorFromSdf(sdf, SrcA[xy]);
    }
}

[numthreads(THREADSX, THREADSY, 1)]
void EllipseI(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float2 center = ScalarD.xy;
        float2 radius = ScalarD.zw;
        float sdf = EllipseSdf(xy, center, radius);
        Dst[xy] = ColorFromSdf(sdf, Dst[xy]);
    }
}

[numthreads(THREADSX, THREADSY, 1)]
void Rectangle(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float2 p = ScalarD.xy;
        float2 q = ScalarD.zw;
        float sdf = RectangleSdf(xy, p, q);
        Dst[xy] = ColorFromSdf(sdf, SrcA[xy]);
    }
}

[numthreads(THREADSX, THREADSY, 1)]
void RectangleI(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float2 p = ScalarD.xy;
        float2 q = ScalarD.zw;
        float sdf = RectangleSdf(xy, p, q);
        Dst[xy] = ColorFromSdf(sdf, Dst[xy]);
    }
}

[numthreads(THREADSX, THREADSY, 1)]
void Line(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float width = ScalarC.y;
        float2 p = ScalarD.xy;
        float2 q = ScalarD.zw;
        float sdf = LineSdf(xy, p, q, width);
        Dst[xy] = ColorFromSdf(sdf, SrcA[xy]);
    }
}

[numthreads(THREADSX, THREADSY, 1)]
void LineI(uint2 xy : SV_DispatchThreadID) {
    if (InRange(xy)) {
        float width = ScalarC.y;
        float2 p = ScalarD.xy;
        float2 q = ScalarD.zw;
        float sdf = LineSdf(xy, p, q, width);
        Dst[xy] = ColorFromSdf(sdf, Dst[xy]);
    }
}
